// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

togglemainmenu = [
    if (! (cleargui 1)) [
        showgui main
    ]
]

bind ESCAPE [togglemainmenu]

selplayermodel = [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel]
newgui main [
    guilist [
    	guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [selplayermodel] 0.9
        guistrut 0.25
        guilist [
            newname = (getname)
            guifield newname 15 [name $newname]
            guispring
            guilist [
                guibutton (concatword "^f8" (playermodelname)) [selplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui crosshair] 0.5
            ]
        ]
    ]
    guibar
    guibutton "server browser.."  [showgui "servers"] "server_browser.png"

    if (isconnected) [
        guibar
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.." [showgui "editing"]
        ]
        guibutton "vote game mode / map.." "showgui gamemode"
        // guibutton "lanconnect"
        // guibutton "connect localhost"

        if (&& (m_teammode (getmode)) (isconnected)) [
            guibutton (concat "switch to team" (at ["3evil" "1good"] (indexof ["good" "evil"] (getteam)))) [if (strcmp (getteam) "good") [team evil] [team good]]
        ]
        guibutton "toggle spectator" [spectator (! (isspectator (getclientnum)))] "spectator"
        guibutton "master.." [showgui "master"]
        if (= (getclientnum) -1)[
            guibutton "demo editor.." [showgui "demomanage"]
        ]
        guibutton "options.." [showgui "options"] "gear.png"
        guibar
        guibutton "disconnect" [disconnect] "exit"
    ] [
        guibutton "bot match.." [showgui "botmatch"] "npc.png"
        if (! $isconnected)[guibutton "map editor" [newmap; echo "^f2Press ^f1F7 ^f2to open the editing menu and ^f1E ^f2to enter or leave edit mode"]]
        guibar
        guibutton "options.." [showgui "options"] "gear.png"
        guibutton "about.." [showgui "about"]
        guibutton "quit" [
            if (=s (getalias quitconfirm) "")[quitconfirm = 1]
            if (quitconfirm) [
                newgui "quit" [
                    guitext "are you sure you want to quit?"
                    guialign 0 [
                        guibutton "return" [cleargui 1] "arrow_bw"
                        guibar
                        guibutton "quit" [quit] "exit"
                    ]
                    guicheckbox "always ask" quitconfirm
                ] 0
                showgui "quit"
            ] [quit]
        ] "exit"
    ]
] 0

newgui about [
    guitext "^f6Tomatenquark ^f~is a fork of ^f6Cube 2: Sauerbraten"
    guitext "brought by the Tomatenquark team, based on the"
    guitext "efforts of ^f8Wouter ^"Aardappel^" van Oortmerssen^f~,"
    guitext "^f8Lee ^"eihrul^" Salzman ^f~and many more."
    guitext "(for a full list of contributors see the readme)"
    guitext "^f1https://tomatenquark.org/"
    guitext "^f1http://sauerbraten.org/"
    guitext "^f1http://cubeengine.com/"
] "^f8about"

genmapitems = [
    looplist curmap $arg1 [
        guibutton $curmap (concat map $curmap) "cube"
    ]
]
//17

ffamaps1 = "frag-lab valkyria blank"
ffamaps2 = ""
capturemaps1 = "luna collide mc-lab pandora"
capturemaps2 = ""
ctfmaps1 = "breakout luna collide mc-lab hurricane3 pandora"
ctfmaps2 = ""
// conceptmaps = "ladder spiralz canyon secondevermap firstevermap door_demo box_demo platform_demo"
// spmaps = "mpsp6a mpsp6b mpsp6c mpsp9a mpsp9b mpsp9c mpsp10 lost level9 skrsp1 crnsp1"
allmaps = (concat $ffamaps1 $ffamaps2 $capturemaps1 $capturemaps2 $ctfmaps1 $ctfmaps2)

showmapshot = [
    guibar
    guiimage (concatword "packages/base/" (if (> $numargs 0) [result $arg1] [at $guirollovername 0]) ".jpg") $guirolloveraction 4 1 "data/cube.png"
]

newgui maps [
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $ffamaps1 ]
      guilist [ guistrut 15 1; genmapitems $ffamaps2 ]
      showmapshot
    ]
    guitab "^f8capture"
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $capturemaps1 ]
      guilist [ guistrut 15 1; genmapitems $capturemaps2 ]
      showmapshot
    ]
    guitab "^f8ctf"
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps1 ]
        guilist [ guistrut 15 1; genmapitems $ctfmaps2 ]
        showmapshot
    ]
] "^f8maps"

newgui capturemaps [
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $capturemaps1 ]
      guilist [ guistrut 15 1; genmapitems $capturemaps2 ]
      showmapshot
    ]
] "^f8capture maps"

newgui ctfmaps [
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps1 ]
        guilist [ guistrut 15 1; genmapitems $ctfmaps2 ]
        showmapshot
    ]
] "^f8ctf maps"

custommaps = ""
newgui custommaps [
    looplist curmap $custommaps [
        guibutton $curmap (concat map $curmap) "cube"
    ]
] "^f8custom maps"
showcustommaps = [
    custommaps = ""
    loopfiles curmap "packages/base" "ogz" [
        if (< (indexof $allmaps $curmap) 0) [
            custommaps = (concat $custommaps $curmap)
        ]
    ]
    custommaps = (sortlist $custommaps x y [<=s $x $y])
    showgui custommaps
]
macro playermodelbutton [cleargui 1; playermodel %1]

newgui playermodel [
    guinoautotab [
        guilist [
            guilist [
                loop i $playermodelnum [
                    guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
                ]
            ]
            guibar
            pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
            pmidx = (max 0 (min (- $playermodelnum 1) $pmidx))
            guilist [
                guiplayerpreview $pmidx 0 6 $guirolloveraction 4 1
                guitextbox (playermodelstory $pmidx) 40 9
            ]
        ]
        guilist [
            guicheckbox "force matching player models" forceplayermodels
            guibar
            guicheckbox "always use team skins" teamskins
        ]
        guilist [
            guicheckbox "fullbright" fullbrightmodels 60 0
            if $fullbrightmodels [
                guibar
                guiradio "subtle" fullbrightmodels 60
                guiradio "bright" fullbrightmodels 100
                guiradio "overbright" fullbrightmodels 150
                guiradio "max" fullbrightmodels 200
            ]
        ]
    ]
] "^f8player model"

crosshairs = ["data/crosshair.png data/hit.png"]
findcrosshairs "packages/crosshairs"
findcrosshairs "packages/crosshairs/tomatenquark"
findcrosshairs "packages/crosshairs/freepik"
findcrosshairs "packages/crosshairs/mx"
findcrosshairs "packages/crosshairs/1brunobruce"

newgui crosshair [
    guitext "crosshair size"
    guislider crosshairsize
    loop row (div (+ (listlen $crosshairs) 6) 7) [
        guilist [
            loop col 7 [
                ch = (at $crosshairs (+ (* $row 7) $col))
                if (!=s $ch "") [
                    guiimage (at $ch 0) [
                        cleargui 1
                        loadcrosshair @(at $ch 0) 0
                        loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
                        echo (concat "^f7new crosshair set:" @(at $ch 0))
                    ] 0.5
                ]
            ]
        ]
    ]
] "^f8crosshair"
getpriv = [
    _mmpriv = (concat "^f7" 0) //white common
    if (ismaster $arg1) [_mmpriv = (concat "^f0" 1)] //green master
    if (isadmin $arg1) [_mmpriv = (concat "^f6" 2)] //orange admin
    if (isauth $arg1) [_mmpriv = (concat "^f5" 3)] //purple auth
    result $_mmpriv
]
haspriv = [if (|| (ismaster $arg1) (isadmin $arg1)) [result 1] [result 0]]
mmclient = [
    guiitemmove -1 -1 -0.5 [
        guilist [
            guibar
            guibutton (concatword (format "^f%1(%2)^f7" (at ["5" "4"] (= (getfollow) @cn)) (getclientnum @cn)) (at (getpriv @cn) 0) (getclientname @cn) (at [@(result "[^t^t]") @(result "[^t]")] (= (strlen (getclientname @cn)) 15))) [follow @@cn] (getclienticon @cn)
        ]
        guiitemmove 0 0 -1 [
            guilist [if (haspriv (getclientnum)) [guitext (gentabs 5) 0][guitext (gentabs 6) 0]; guibar]
        ]
    ]
    guiitemmove -1 0 -1.5 [guitext $mmtbs 0; guibar]
    if (haspriv (getclientnum)) [
        guiitemmove -1 21 -1.5 [
            guilist [
                guibutton "kick " [kick @@cn]
                guibar
                guibutton (at [@(result "[spec  ]") @(result "[unspec]")] (isspectator @cn)) [spectator @@(= (isspectator @@@cn) 0) @@cn]
                if (m_teammode (getmode)) [
                    guibar
                    guibutton "switch team " [setteam @@cn @@(? (=s (getclientteam @@@cn) "good") "evil" "good")]
                ]
                guibar
                if (ismaster @cn) [
                    guibutton "take master" [setmaster 0 @@cn]
                ] [
                    guiitemmove 0 0 0.15 [
                        guibutton "give master" [setmaster 1 @@cn]
                    ]
                ]
                if (m_teammode (getmode))[guiitemmove 0 0 1.87 [guitext "" 0; guibar]] [guiitemmove 0 0 0.55 [guitext "" 0; guibar]]
            ]
        ]
    ]
]

_splitspec = 0
newgui master [
    if (! $isconnected)[
        guialign 0 [guitext "^f3you have been disconnected, kicked or banned."]
        guibutton "server browser.." [showgui "servers"]
        guibutton "bot match.." [showgui "botmatch"]
        guibar
        guibutton "reconnect" [reconnect]
        guibutton "main" [cleargui] "arrow_bw"
    ] [
        guistayopen [
        guilist [guistrut 60]        
        guiitemmove 1 0 -1.7 [
            if (&& (isconnected) (!=s $connectedip "")) [
                guitext (concatword $connectedip ":" $connectedport) 0
            ]
        ]
        guialign 1 [
            if (ismaster (getclientnum)) [
                guibutton "relinquish master" [setmaster 0]
            ] [
                guibutton "claim master" [if (getalias _privpass)[setmaster $_privpass][setmaster 1]]
            ]
        ]
        guiitemmove -1 0 -1 [guialign -1 [guitext "^f7password:^t" 0; guifield _privpass -10]]
        guiitemmove 0 0 -1 [
            guitext (concatword (at (getpriv (getclientnum)) 0) (at ["no privileges" "master" "admin" "auth"] (at (getpriv (getclientnum)) 1))) 0
        ]
        mm = (getmastermode)
        mmlist = ["open" "veto" "locked" "private"]
        mmdesc = [
            "any client can join and claim master privileges if available."
            "2all clients are able to vote for a map."
            "6when joining, the client is forced to spectate."
            "3other clients are unable to join without a server password or auth."
        ]

        guialign 0 [
            guititle "^t"
            looplist m $mmlist [
                guiradio (format "^f7(%1%2^f7)%1%3" (at [ @(result (at ["4" "7"] (|| $isadmin $ismaster))) @(result "^f8")] (=s $mm (indexof $mmlist $m))) (indexof $mmlist $m) $m) mm (indexof $mmlist $m) [if (! $setmaster)[setmaster 1]; mastermode (indexof $mmlist @m)]
            ]
        ]
        guiitemmove 0 8 0 [guitextbox (at $mmdesc $mm) 38 2 "0xAAFFFF"]
        guibar
        guiitemmove -1 0 -0.5 [
            if (haspriv (getclientnum)) [
                guialign -1 [
                    guibutton (at [ @(result "[1pause game^t]") @(result "[6unpause game^t]")] $paused) [if (= $paused 1) [pausegame 0][pausegame 1]]
                    guibar
                    guibutton "clearbans^t" [clearbans]
                    guibar
                    guibutton "record demo" [recorddemo 1]
                ]
            ]
            if (|| $editing (m_edit (getmode))) [
                guialign -1 [
                    guibutton "start newmap^t" [if (= $editing 0)[edittoggle; savemap backup; newmap; edittoggle][newmap]]
                    guibar
                    guibutton "show custom maps.." [showcustommaps]
                ]
                guialign -1 [
                    guibutton "savemap^t^t" [savemap]
                    guibar
                    guibutton "sendmap^t" [sendmap]
                    guibar
                    guibutton "getmap" [getmap]
                ]
            ]
            if (! (haspriv (getclientnum)))[guistrut 1]
        ]

        if (> (listlen (listclients 0 0)) 0) [
            //Players
            guitab (concatword "^f8players^f7(" (at [@(result (listlen (listclients 0 0))) @(result (at (numplayers) 0))] $_splitspec) ")")
            if (haspriv (getclientnum)) [if (m_teammode (getmode)) [mmtbs = (gentabs 17)] [mmtbs = (gentabs 13)]] [mmtbs = (gentabs 6)]

            if (> (at (numplayers) 0) 0) [
                guitext (concatword "^f8playing^f7: " (at (numplayers) 0)) 0
                if (haspriv (getclientnum))[
                    guiitemmove 1 0 -1 [guibutton "specall" [looplist c (listclients 0 0) [spectator 1 $c]] "spectator"]
                ]
                guiitemmove -1 0 -1.4 [guitext $mmtbs 0; guibar]
                looplist cn (listclients 0 0) [
                    if (! (isspectator $cn))[@@mmclient]
                ]
            ] [guialign 0 [guitext "^f7no active players"]; guibar]

            //Spectators
            if (> (at (numplayers) 1) 0) [
                if (_splitspec)[guitab (concatword "^f8spectators^f7(" (at (numplayers) 1) ")") ]

                if (|| (haspriv (getclientnum)) (= $_splitspec 1) (= (at (numplayers) 0) 0)) [
                    guitext (concatword "^f8watching^f7: " (at (numplayers) 1)) 0
                    guiitemmove -1 10 -1 [guicheckbox "split tabs" _splitspec]
                    if (haspriv (getclientnum))[
                        guiitemmove 1 0 -1 [guibutton "unspecall" [looplist c (listclients 0 0) [spectator 0 $c]] "spectator"]
                    ]
                    guiitemmove -1 0 -1.4 [guitext $mmtbs 0; guibar]
                    looplist cn (listclients 0 0) [
                        if (isspectator $cn)[@@mmclient]
                    ]
                ] [
                    guiitemmove 1 0 (concatword "-" (+ (at (numplayers) 0) 1) ".638") [
                        guitext (concatword "^f8watching^f7: " (at (numplayers) 1)) 0
                        guiitemmove -1 10 -1 [guicheckbox "split tabs" _splitspec]
                        guiitemmove -1 0 -1.4 [guitext $mmtbs 0; guibar]
                        looplist cn (listclients 0 0) [
                            if (isspectator $cn)[@@mmclient]
                        ]
                    ]
                ]
            ]
        ]
    ]
    ]
] "^f7server"

botmatchcount = 5
botmatchminskill = 50
botmatchmaxskill = 100

startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $botmatchcount [addbot (rnd (+ $botmatchmaxskill 1) $botmatchminskill)]
    ]
]

newgui botmatch [
    guitext "bot amount"
    guislider botmatchcount 0 32
    guitext "bot minimum skill"
    guislider botmatchminskill 50 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
    guitext "bot maximum skill"
    guislider botmatchmaxskill 50 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
    guibutton "start match.." [guionclear [startbotmatch]; showgui gamemode]
] "^f8bot match"

newgui campaign [
    guibutton "start Private Stan Sauer"           "showgui privatestansauer"
    guibutton "start An Army Of One"               "showgui armyofone"
    guibutton "start Lost"                         "sp lost" "cube"
    guibutton "start Meltdown"                     "sp skrsp1" "cube"
    guibutton "start Missile Pass"                 "sp crnsp1" "cube"
    guibutton "start Level 9"                      "sp level9" "cube"
    guibar
    guibutton "start DMSP map.."                   "mode -2; showgui maps"
    guicheckbox "slow motion"                      "slowmosp"
    guitext   "skill (default: 3)"
    guislider skill
]

newgui armyofone [
    guilist [
        guilist [
            guibutton "Part I" "sp mpsp6a" "cube"
            guibutton "Part II" "sp mpsp6b" "cube"
            guibutton "Part III" "sp mpsp6c" "cube"
        ]
        showmapshot (substr $guirolloveraction 3)
    ]
] "An Army Of One"

newgui privatestansauer [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I" "sp mpsp9a" "cube"
            guibutton "Run N' Gun Part II" "sp mpsp9b" "cube"
            guibutton "Run N' Gun Part III" "sp mpsp9c" "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" "sp mpsp10" "cube"
        ]
        showmapshot (substr $guirolloveraction 3)
    ]
] "Private Stan Sauer"

newgui _lanserver [
    guistayopen [
        guialign -1 [
            guitext "server name:^t"
            guifield serverdesc -10
            guibar
            guitext "server port:^t"
            guifield serverport -10
        ]
        guialign -1 [
            guitext "^f6admin ^f~pass:^t"
            guifield adminpass -10
            guibar
            guitext "^f0master ^f~pass:^t"
            guifield adminpass -10
        ]
        guialign -1 [
            guitext "max clients:^t"
            guifield maxclients -10
            guibar
            guicheckbox "public server" publicserver
        ]
        guibar
        guialign 0 [
            guibutton "return" [cleargui 1] "arrow_bw"
            guibar
            guitext "LAN:^t"
            guibutton "^f1start" [startlistenserver] 0
            guitext " / " 0
            guibutton "^f3stop" [stoplistenserver] 0
            guitext " / " 0
            guibutton "^f0connect" [lanconnect] 0
        ]
    ]
] 0

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update from master server" [updatefrommaster]
                guibar
                guicheckbox "search LAN" [searchlan]
                guibar
                guicheckbox "auto-update" [autoupdateservers]
                guibar
                guicheckbox "auto-sort" [autosortservers]
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" [sortservers]
                ]
                guibar
                guibutton "reconnect" [reconnect]
                guibar
                guibutton "LAN server" [showgui _lanserver] "ping.png"
            ]
            guibar
        ] 17
    ]
] "^f8servers" [initservers]

newgui gamemode [
    guilist [
        guilist [
            guibutton "ffa"                "mode 0; showgui maps"
            guibutton "coop-edit"          "mode 1; showgui maps"
            guibutton "teamplay"           "mode 2; showgui maps"
            guibutton "instagib"           "mode 3; showgui maps"
            guibutton "instagib team"      "mode 4; showgui maps"
            guibutton "efficiency"         "mode 5; showgui maps"
            guibutton "efficiency team"    "mode 6; showgui maps"
            guibutton "tactics"            "mode 7; showgui maps"
            guibutton "tactics team"       "mode 8; showgui maps"
            guibutton "capture"            "mode 9; showgui capturemaps"
            guibutton "regen capture"      "mode 10; showgui capturemaps"
        ]
        guibar
        guilist [
            guibutton "ctf"                "mode 11; showgui ctfmaps"
            guibutton "insta ctf"          "mode 12; showgui ctfmaps"
            guibutton "efficiency ctf"     "mode 17; showgui ctfmaps"
            guibutton "protect"            "mode 13; showgui ctfmaps"
            guibutton "insta protect"      "mode 14; showgui ctfmaps"
            guibutton "efficiency protect" "mode 18; showgui ctfmaps"
            guibutton "hold"               "mode 15; showgui capturemaps"
            guibutton "insta hold"         "mode 16; showgui capturemaps"
            guibutton "efficiency hold"    "mode 19; showgui capturemaps"
            guibutton "collect"            "mode 20; showgui ctfmaps"
            guibutton "insta collect"      "mode 21; showgui ctfmaps"
            guibutton "efficiency collect" "mode 22; showgui ctfmaps"
        ]
    ]
] "^f8gamemode"

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guilist [
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "exec" [textfocus @arg1; textexec]
                guibar
                guibutton "copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "select" [textfocus @arg1; textselectall]
                guibar
                guibutton "clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
] "^f8notepad"

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui scratchpad]

newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "toggle edit mode	(E)"              "edittoggle"
    guitext "toggle heightmap	(H)"
    guibutton "deselect			(SPACE)"                "cancelsel"
    guibutton "passthrough		(hold LCTRL)"               "passthrough"
    guitext "select			drag left mouse   "
    guitext "select corners		drag middle mouse "
    guitext "extend selection	right mouse"
    guitext "reorient			right mouse"
    guitext "move selection		drag right mouse"
    guibutton "copy				(C)"			                  "copy"
    guibutton "paste			(V)"			                "paste"

    guitab "^f8entities"
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guicheckbox "mapmodel warnings"          "mapmodelwarnings"
        guibutton "light.."                      "resetlight;showgui newlight"

        guibutton "newent playerstart   "
        guibutton "newent shells"
        guibutton "newent bullets"
        guibutton "newent rockets"
        guibutton "newent riflerounds"
        guibutton "newent grenades"
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
        guibutton "monster.."                    "showgui monsters"
        guibutton "trigger.."                    "showgui triggers"

        guibutton "newent cartridges"
        guibutton "newent health"
        guibutton "newent healthboost"
        guibutton "newent greenarmour"
        guibutton "newent yellowarmour"
        guibutton "newent quaddamage"
      ]
    ]

    guitab "^f8keys"
    guibutton "flip / mirror cube		(X)"            "flip"
    guibutton "undo					(Z, U)"                     "undo"
    guibutton "redo					(I)"                        "redo"
    guibutton "delete				(BACKSPACE)"                  "editdel"
    guibutton "texture palette     	(F2)" "showtexgui"
    guibutton "edit entity gui			(F3)" "showentgui"
    guibutton "mapmodels list			(F4)" "showgui mapmodels"
    guibutton "select all in box		(keypad ENTER)" "entselect insel"
    guibutton "select all matching		(N)" "selentfindall"
    guibutton "center view on ent		(COMMA + scroll)" "entautoview"
    guibutton "edit entity console		(PERIOD)" "selentedit"
    guitext "toggle heightmap tex	(MMB in heightmapmode)"
    guicheckbox "toggle outline			(7)"        outline
    guicheckbox "toggle wireframe		(8)"    wireframe


    guitab "^f8scroll"
    guitext "push/pull selection			scroll wheel only"
    guitext "push the face of a cube	hold F + scroll wheel"
    guitext "change gridsize			hold G + scroll wheel"
    guitext "change textures			hold Y + scroll wheel"
    guitext "rotate         			hold R + scroll wheel"
    guitext "push corner with cursor		hold Q + scroll wheel"
    guitext "push/pull heightmap			hold LALT + scroll wheel"
    guitext "change heightmap brush		hold B + scroll wheel"

    guitab "^f8map lighting"
    guibutton "calclight 1 (slow: 8xAA)" "calclight 1"
    guibutton "calclight -1 (quick: no AA, no model shadows)"   "calclight -1"
    guibutton "patchlight"                         "patchlight"

    guicheckbox "fullbright" fullbright

    guitext "lightprecision (default: 32)"
    guilistslider lightprecision "8 16 32 48 64 128 256"

    guitext "lighterror (default: 8)"
    guislider lighterror

    guitext "lightthreads (CPU threads/cores) (default: 0)"
    guislider lightthreads

    //guibutton "lightlod high (6)" "lightlod 6"
    //guibutton "lightlod low  (2)" "lightlod 2"
    //guibutton "lightlod off  (0)" "lightlod 0"


    guitab "^f8texture blend"
    guitext "blend paint mode:"
	guiradio "off						(keypad 0)" blendpaintmode 0
	guiradio "replace					(keypad 1)" blendpaintmode 1
	guiradio "dig						(keypad 2)" blendpaintmode 2
	guiradio "fill						(keypad 3)" blendpaintmode 3
	guiradio "inverted dig				(keypad 4)" blendpaintmode 4
	guiradio "inverted fill				(keypad 5)" blendpaintmode 5
    guibar
    guitext "paint blendmap			(MOUSE1)"
	guibutton "change blend brush			scroll wheel" "nextblendbrush"
	guibutton "rotate blend brush			(MOUSE2)" "rotateblendbrush"
	guibutton "clear entire blendmap 		(^"clearblendmap^" command)" "clearblendmap"
	guibutton "clear blendmap in selection	(^"clearblendmapsel^" command)" "clearblendmapsel"

    guitab "^f8map"
    guitext   "save map:"
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/maptitle ]"
    guibutton "show custom maps"                   "showcustommaps"

    guitab "^f8skybox"
    guilist [
        guistayopen [
            guilist [ genskyitems $skies1 ]
            showskyshot
        ]
    ]

    guitab "^f8cfg"
    guitext (mapcfgname)
    showfileeditor (mapcfgname) -70 15
] "^f8editing"


skies1 = [
skyboxes/cruz/amazon
skyboxes/cruz/antartic
skyboxes/cruz/ethna
skyboxes/cruz/myth
skyboxes/cruz/rockwell
skyboxes/cruz/rune
skyboxes/djtex/isle
skyboxes/djtex/isless
skyboxes/djtex/mountains
penguins/arid
penguins/barren
penguins/divine
penguins/gloom
penguins/harmony
penguins/wrath
penguins/yonder
penguins/zeus
skyboxes/lordkv/kvstar1K
]

genskyitems = [
    looplist cursky $arg1 [
        guibutton $cursky (concat skybox $cursky) //"cube"
    ]
]

alias showskyshot [
    _curskythumb = (concatword (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) "_ft.");
    guibar
    guiimage (concatword "<thumbnail:128>packages/" $_curskythumb (at ["jpg" "png"] (findfile (concatword "packages/" $_curskythumb "png")) )) $guirolloveraction 4 1 "data/cube.png"
]


newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high		(3)"  "waterlod 3"
    //guibutton "waterlod high		(2)"          "waterlod 2"
    //guibutton "waterlod default		(1)"  "waterlod 1"
    //guibutton "waterlod low		(0)"          "waterlod 0"

    //guibutton "watersubdiv very fine	(0)"          "watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"  "watersubdiv 1"
    //guibutton "watersubdiv default		(2)"  "watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"  "watersubdiv 3"

    //guibutton "water waves on"                     "waterwaves 1"
    //guibutton "water waves off"                    "waterwaves 0"
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

newgui triggers [
    guibutton [invisible]               [BTmm = -1; showgui trigger2]
    guibutton [a carrot]                [BTmm = 23; showgui trigger2]
    guibutton [switch #1]               [BTmm = 24; showgui trigger2]
    guibutton [switch #2]               [BTmm = 25; showgui trigger2]
    guibutton [door #1]                 [BTmm = 26; showgui trigger2]
    guibutton [door #2]                 [BTmm = 27; showgui trigger2]
    guibutton [door #3]                 [BTmm = 28; showgui trigger2]
]

newgui trigger2 [
    guibutton [animation loop]                      [BTtt = 0; showgui trigger3]
    guibutton [trigger once (popback)]              [BTtt = 1; showgui trigger3]
    guibutton [trigger once (popback & rumble)]     [BTtt = 2; showgui trigger3]
    guibutton [trigger once]                            [BTtt = 3; showgui trigger3]
    guibutton [trigger once (rumble)]               [BTtt = 4; showgui trigger3]
    guibutton [trigger multiple (popback)]          [BTtt = 5; showgui trigger3]
    guibutton [trigger multiple (popback & rumble)] [BTtt = 6; showgui trigger3]
    guibutton [trigger multiple]                        [BTtt = 7; showgui trigger3]
    guibutton [trigger multiple (rumble)]           [BTtt = 8; showgui trigger3]
    guibutton [door (open once)]                        [BTtt = 9; showgui trigger3]
    guibutton [door (auto close)]                   [BTtt = 10; showgui trigger3]
    guibutton [door (locked)]                       [BTtt = 11; showgui trigger3]
    guibutton [vanishing trigger]                   [BTtt = 12; showgui trigger3]
    guibutton [vanishing trigger (rumble)]          [BTtt = 13; showgui trigger3]
    guibutton [End Of Level Trigger]                    [BTtt = 29; showgui trigger3]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    loop i 9 [
        tjt = (concat tag (concatword # $i))
        tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $tjt $tjc
    ]
]


resetlight = [
    lightcolour = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
]

lightset = [
    lightr = $arg1
    lightg = $arg2
    lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
        lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
    ] [
        lightr = (min 255 (* $lightr 2)); lightg = (min 255 (* $lightg 2)); lightb = (min 255 (* $lightb 2))
    ]
]

lightcmd = [
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 255 255"
    guibutton (lightcmd)
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $lightr 256) $lightg) 256) $lightb)
    guislider lightr 0 255
    guislider lightg 0 255
    guislider lightb 0 255
    guilist [
        guicheckbox "bright"    lightbright 1 0 [lightscale]
        guibar
        guiradio "white"        lightcolour 0 [lightset 255 255 255]
        guiradio "blue"         lightcolour 1 [lightset 192 192 255]
        guiradio "red"          lightcolour 2 [lightset 255 192 192]
        guiradio "green"        lightcolour 3 [lightset 192 255 192]
        guiradio "yellow"       lightcolour 4 [lightset 255 255 192]
        guiradio "purple"       lightcolour 5 [lightset 255 192 255]
        guiradio "turquoise"    lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

newgui mapmodels [
    loop i (div (+ (nummapmodels) (- 17 1)) 17) [
        if (> $i 0) [
            guitab (concatword "^f8" (+ $i 1))
        ]
        guilist [
            guilist [
                loop j (min (- (nummapmodels) (* $i 17)) 17) [
                    mmidx = (+ (* $i 17) $j)
                    mmname = (mapmodelname $mmidx)
                    if (= (strstr $mmname "mapmodels") 0) [
                        mmname = (substr $mmname 10)
                    ]
                    guibutton $mmname [newent mapmodel @mmidx]
                ]
            ]
            guispring
            guibar
            guimodelpreview (mapmodelname (at $guirolloveraction 2)) "mapmodel" $guirolloveraction 4 1
        ]
    ]
] "^f8mapmodels"

newgui monsters [
    guibutton "ogro / fireball"      "newent monster 0"
    guibutton "rhino / chaingun"      "newent monster 1"
    guibutton "ratamahatta / shotgun"      "newent monster 2"
    guibutton "slith / rifle" "newent monster 3"
    guibutton "bauul / RL"     "newent monster 4"
    guibutton "hellpig / bite"       "newent monster 5"
    guibutton "knight / iceball" "newent monster 6"
    guibutton "goblin / slimeball"   "newent monster 7"
    guibutton "spider / grenade"    "newent monster 8"
] "^f8monsters"

newgui postfx [
    guibutton "(effect OFF)"          "clearpostfx"
    guibutton "bloom (subtle: 30%)"    "bloom 0.3"
    guibutton "bloom (bright: 55%)"    "bloom 0.55"
    guibutton "bloom (intense: 80%)"  "bloom 0.8"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"])
]

newgui options [
    guialign -1 [
        guicheckbox "^f0Allow custom content from servers" downloadmaps
        guistrut 1
        guibutton "About custom content" [showgui _aboutcc]
    ]
    guicheckbox "show FPS" showfps
    guilist [
        guicheckbox "wall clock" wallclock
        if $wallclock [
            guibar
            guicheckbox "24 hour" wallclock24
            guibar
            guicheckbox "seconds" wallclocksecs
        ]
    ]
    //guicheckbox "2D menus" gui2d
    guicheckbox "show scoreboard at death" deathscore
    guilist [
        guicheckbox "damage screen"     damagescreen
        guibar
        guicheckbox "damage compass"    damagecompass
    ]
	guilist [
    	guicheckbox "hudguns"       hudgun
		if $hudgun [
			guibar
			guicheckbox "sway"		hudgunsway
            guibar
            guicheckbox "muzzle flash" muzzleflash
            guibar
            guicheckbox "muzzle light" muzzlelight
		]
	]
    guicheckbox "blood"             blood
    guilist [
        guicheckbox "ragdoll deaths"    ragdoll
        if $ragdoll [
            guibar
            guicheckbox "keep after respawn" ragdollmillis 10000
        ]
        guibar
        guicheckbox "hide dead players" hidedead
    ]
    guitext "ragdoll velocity multiplier"
    guislider deadpush
    //guilist [
    //    guicheckbox "fullbright player models" fullbrightmodels 60 0
    //    if $fullbrightmodels [
    //        guibar
    //        guiradio "subtle" fullbrightmodels 60
    //        guibar
    //        guiradio "bright" fullbrightmodels 100
    //        guibar
    //        guiradio "overbright" fullbrightmodels 150
    //        guibar
    //        guiradio "max" fullbrightmodels 200
    //    ]
    //]
    //guilist [
    //    guicheckbox "force matching player models" forceplayermodels
    //    guibar
    //    guicheckbox "always use team skins" teamskins
    //]
    guilist [
        guicheckbox "outline capture meters" outlinemeters
        guibar
        guicheckbox "numbered capture bases" basenumbers
        guibar
        guicheckbox "show teammates" radarteammates 1
    ]
    guicheckbox "hit sound" hitsound
    guilist [
        guicheckbox "crosshair color effects" crosshairfx
        if $crosshairfx [
            guibar
            guicheckbox "teammates" teamcrosshair
            guibar
            guicheckbox "hits" hitcrosshair 425
        ]
    ]
    guibutton "^f3restore defaults (clears ALL settings)" "exec restore.cfg"

    guitab "^f8gfx"
    guitext "performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guitext "" 0
	guilist [
       	guitext "shaders^t^t"
        guiradio "^f0low detail^t^t" shaderdetail 0
        guiradio "^f2medium detail^t" shaderdetail 2
        guiradio "^f3high detail" shaderdetail 3
	]
    guilist [
        guitext "water^t^t"
        guicheckbox "^f2refraction^t^t"  waterrefract
        guicheckbox "^f3reflection^t^t"  waterreflect
        guicheckbox "^f0caustics^t"    caustics
        guicheckbox "^f0animation" vertwater
    ]
	guilist [
		guitext "waterfalls^t^t"
		guicheckbox "^f2refraction^t^t" waterfallrefract
		guicheckbox "^f0reflection" waterfallenv
	]
	guilist [
       	guicheckbox "^f3soft shadows^t"  shadowmap
		if $shadowmap [
            guiradio "^f0low detail^t^t" shadowmapsize 7 [blurshadowmap 1]
      	    guiradio "^f2medium detail^t" shadowmapsize 9 [blurshadowmap 1]
            guiradio "^f3high detail" shadowmapsize 11 [blurshadowmap 0]
		] [
			guicheckbox "^f0blob shadows" blobs
		]
   	]
   	if $glare [
       	glarepreset = 0
       	if (= $glarescale 1) [
           	if (= $blurglare 4) [glarepreset = 1]
           	if (= $blurglare 7) [glarepreset = 3]
       	]
       	if (= $glarescale 2) [
           	if (= $blurglare 3) [glarepreset = 2]
           	if (= $blurglare 7) [glarepreset = 4]
       	]
       	guilist [
           	guicheckbox "^f3glare^t^t^t"       glare
           	guiradio "^f2subtle^t^t" glarepreset 1 [blurglare 4; glarescale 1]
           	guiradio "^f2glowy^t^t" glarepreset 2 [blurglare 3; glarescale 2]
           	guiradio "^f3soft^t^t" glarepreset 3 [blurglare 7; glarescale 1]
           	guiradio "^f3intense" glarepreset 4 [blurglare 7; glarescale 2]
       	]
   	] [
        guilist [
       	    guicheckbox "^f3glare^t^t^t"         glare
        ]
   	]
    guilist [
        guicheckbox "^f3motion blur^t" motionblur
        if $motionblur [
            guiradio "^f3subtle^t^t" motionblurscale 0.5
            guiradio "^f3moderate^t^t" motionblurscale 0.65
            guiradio "^f3intense" motionblurscale 0.8
        ]
        
    ]
    guilist [
        guicheckbox "^f3grass^t^t^t" grass
		if $grass [
			guiradio "^f2quick fade^t" grassdist 128
			guiradio "^f2moderate fade^t" grassdist 256
			guiradio "^f3slow fade" grassdist 512
		]
	]
    guilist [
        guicheckbox "^f0dynamic lights^t"    maxdynlights 3 0
        if $maxdynlights [
            guiradio "^f0medium detail^t" maxdynlights 3
            guiradio "^f2high detail" maxdynlights 5
        ]
    ]
    guilist [
        guicheckbox "^f0soft particles^t" depthfx
        if $depthfx [
            guiradio "^f0low detail^t^t" depthfxsize 7 [blurdepthfx 1]
            guiradio "^f2medium detail^t" depthfxsize 10 [blurdepthfx 0]
            guiradio "^f3high detail" depthfxsize 12 [blurdepthfx 0]
        ]
	]
    guilist [
        guicheckbox "^f0glass reflection^t"  glassenv
    ]
    guilist [
        guicheckbox "^f0decals^t^t" decals
        if $decals [
            guiradio "^f0quick fade^t" decalfade 10000 [maxdecaltris 1024]
            guiradio "^f2slow fade" decalfade 60000 [maxdecaltris 4096]
        ]
    ]
        guicheckbox "^f0fix t-joints^t" filltjoints
    guilist [
        guitext "textures^t^t"
        guiradio "^f0low detail^t^t" maxtexsize 256
        guiradio "^f0medium detail^t" maxtexsize 512
        guiradio "^f2high detail" maxtexsize 0
    ]
    guilist [
        guitext "models^t^t"
        guicheckbox "^f0lighting^t^t"    lightmodels
        guicheckbox "^f0reflection^t^t"  envmapmodels
        guicheckbox "^f0glow^t^t"        glowmodels
        guicheckbox "^f2bumpmap" bumpmodels
    ]
    guitab    "^f8display"
    guicheckbox "v-sync"            vsync 1 0
    guilist [
        guicheckbox "fullscreen " [fullscreen]
        guistayopen [guibutton "toggle and adjust" [togglefullscreen]]
    ]
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 16 24 32"
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
	guilist [
    	guicheckbox "bilinear filtering" bilinear
		guibar
		guicheckbox "trilinear filtering (mipmaps)" trilinear
	]

    guitab   "^f8resolution"
    guitext "field of view (default: 100)"
    guislider fov
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @@@@(resbutton 320 240)
            @@@@(resbutton 640 480)
            @@@@(resbutton 800 600)
            @@@@(resbutton 1024 768)
            @@@@(resbutton 1152 864)
            @@@@(resbutton 1280 960)
            @@@@(resbutton 1400 1050)
            @@@@(resbutton 1600 1200)
            @@@@(resbutton 1792 1344)
            @@@@(resbutton 1856 1392)
            @@@@(resbutton 1920 1440)
            @@@@(resbutton 2048 1536)
            @@@@(resbutton 2800 2100)
            @@@@(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @@@@(resbutton 320 200)
            @@@@(resbutton 640 400)
            @@@@(resbutton 1024 640)
            @@@@(resbutton 1280 800)
            @@@@(resbutton 1440 900)
            @@@@(resbutton 1600 1000)
            @@@@(resbutton 1680 1050)
            @@@@(resbutton 1920 1200)
            @@@@(resbutton 2048 1280)
            @@@@(resbutton 2560 1600)
            @@@@(resbutton 2880 1800)
            @@@@(resbutton 3200 2000)
            @@@@(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @@@@(resbutton 1024 600)
            @@@@(resbutton 1280 720)
            @@@@(resbutton 1366 768)
            @@@@(resbutton 1600 900)
            @@@@(resbutton 1920 1080)
            @@@@(resbutton 2048 1152)
            @@@@(resbutton 2560 1440)
            @@@@(resbutton 2880 1620)
            @@@@(resbutton 3200 1800)
            @@@@(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @@@@(resbutton 600 480)
            @@@@(resbutton 1280 1024)
            @@@@(resbutton 1600 1280)
            @@@@(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @@@@(resbutton 800 480)
            @@@@(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]

    guitab   "^f8sound"
    guitext "master volume" "sound.png"
    guislider soundvol
    guitext "music volume" "sound.png"
    guislider musicvol
    guitext "sound channels" "sound.png"
    guislider soundchans
    guitext "sound frequency" "sound.png"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length" "sound.png"
    guislider soundbufferlen
    guicheckbox "Mumble positional audio" mumble

    guitab  "^f8mouse"
    guicheckbox "invert mouse"      invmouse

    guilist [
        guitext "mouse sensitivity: " "mouse.png"
        newsens = $sensitivity
        guifield newsens 8 [sensitivity $newsens]
    ]

    guilist [
        guitext "mouse UI sensitivity: " "mouse.png"
        newguisens = $guisens
        guifield newguisens 8 [guisens $newguisens]
    ]

    guilist [
        guibutton "crosshair: " [showgui crosshair]
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guitext "crosshair size"
    guislider crosshairsize

    guitab "^f8keys"
    guitext (concatword "(basic keybinds, for anything more use the '^f1/" $_keytypev " [action]^f~' command)")
    guitext "select action to bind and press desired keys (^f1ESC ^f~when done):"
    guialign 0 [
        guiradio "bind" _keytype 0 [_keytypev = "bind"]
        guiradio "editbind" _keytype 1 [_keytypev = "editbind"] 
        guiradio "specbind" _keytype 2 [_keytypev = "specbind"] 
    ]
    guilistsplit n 2 $(format "%1sactions" $_keytypev) [
        guilist [
            guitext (tabify (concatword $n ": ") 4)
            [newbinds@i] = @(format "(search%1s $n)" $_keytypev)
            guikeyfield [newbinds@i] -9 [
                oldbinds = @(format "(search%1s @@n)" $_keytypev)
                looplist j $oldbinds [$_keytypev $j ""]
                looplist j $[newbinds@@i] [ $_keytypev $j [@@@n]]
            ]
        ]
    ] [guibar]

    guitab "^f8console"
    guicheckbox "chat console" miniconfilter 0x300 0
    guitext "console size (lines)"
    guislider consize
    guitext "console filter:"
    guilist [
        guilist [
            guibitfield "important information" confilter 0x01
            guibitfield "warnings" confilter 0x02
            guibitfield "errors" confilter 0x04
            guibitfield "init messages" confilter 0x10
            guibitfield "script messages" confilter 0x20
	    ]
        guibar
        guilist [
            guibitfield "chat" confilter 0x100
            guibitfield "team chat" confilter 0x200
            guibitfield "game events" confilter 0x400
            guibitfield "self frags" confilter 0x800
            guibitfield "other player frags" confilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "team kills" confilter 0x2000
        ]
    ]
    guitext "full console size (percent of screen)"
    guislider fullconsize
    guitext "full console filter:"
    guilist [
        guilist [
            guibitfield "important information" fullconfilter 0x01
            guibitfield "warnings" fullconfilter 0x02
            guibitfield "errors" fullconfilter 0x04
            guibitfield "init messages" fullconfilter 0x10
            guibitfield "script messages" fullconfilter 0x20
        ]
        guibar
        guilist [
            guibitfield "chat" fullconfilter 0x100
            guibitfield "team chat" fullconfilter 0x200
            guibitfield "game events" fullconfilter 0x400
            guibitfield "self frags" fullconfilter 0x800
            guibitfield "other player frags" fullconfilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "team kills" fullconfilter 0x2000
        ]
    ]

    guitab "^f8autoexec.cfg"
    guitext "autoexec.cfg ^f7- ^f8this file runs automatically when starting"
    showfileeditor "autoexec.cfg" -60 15
] "^f8game"

newgui _aboutcc [
    guitextbox "^f6Tomatenquark ^f~allows a server to use an external host to provide a .zip file with customized content (maps, textures, models, sounds, text files...) that can be downloaded by clients who have ^"^f8downloadmaps^f~^" enabled." 45
    guitextbox "^f6Tomatenquark ^f~apply security filters to remove commands that could possibly damage your client, however the Tomatenquark Team is not responsible for the customized content made available by the server and their licenses." 45
    guibar
    guitextbox "Be aware that, if enabled, ^f8downloadmaps ^f~will require some amount of storage." 45
    guicheckbox "^f0Allow custom content from servers" downloadmaps
    guibutton "back" [cleargui 1] arrow_bw
    guialign 1 [
        guitext "^f7more info at ^f8tomatenquark.org" 0
    ]
] "^f8Server Content"

bindsactions = [forward backward left right jump attack togglezoom saycommand sayteamcommand showscores toggleconsole screenshot edittoggle fastsaw togglefullscreen "setweapon FI" "setweapon SG" "setweapon CG" "setweapon RL" "setweapon RI" "setweapon GL" "setweapon PI" "weapon" "universaldelta 1" "universaldelta -1" dropflag addbot delbot]
editbindsactions = [editflip editpaste cancelsel floatspeedboost showtexgui calclight selentfindall gotosel "domodifier 1" "domodifier 2" "domodifier 3" "domodifier 4" "domodifier 6" "domodifier 9" "domodifier 11" "domodifier 12" "domodifier 13" "domodifier 14" "domodifier 15" "domodifier 16"]
specbindsactions = [follow "nextfollow" "nextfollow -1" floatspeedboost togglezoom]
entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
  entguitype = (enttype)
  @(loopconcat i 5 [result [
    entguiattr@i = (entattr @i)
  ]])
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ] [
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

guilistsplit = [
  guilist [
    i = 0
    l = (listlen $arg3)
    z = (div (+ $l (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $l)
        while [< $i $t] [
          $arg1 = (at $arg3 $i)
          arg4
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton "quick light"       "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent shells"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"
    guicheckbox "show material volumes"    showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "packages/icons/brush_1c.png" brush_0
  guiimage "packages/icons/brush_21c.png" brush_1
  guiimage "packages/icons/brush_421c.png" brush_2
  ]
  guilist [ // squares
  guiimage "packages/icons/brush_3s.png" brush_3
  guiimage "packages/icons/brush_5s.png" brush_4
  guiimage "packages/icons/brush_7s.png" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "packages/icons/brush_3s.png" brush_6
  guiimage "packages/icons/brush_5s.png" brush_7
  guiimage "packages/icons/brush_7s.png" brush_8
  ]
]

newentgui = [
  genentattributes $arg1 $arg2 $arg3
  newgui $arg1 [
    guitext $entguitype
    guibar
    @entattributes
    guitab type
    guilistsplit n 2 $enttypelist [
        guibutton $n [ entset @n ]
    ]
    guitab "^f8misc"
    @quickeditmenu
  ] "" [initentgui]
]

looplist i $enttypelist [
  newentgui $i "" ""
]

newgui materials [
  @matmenu
  guitab "^f8misc"
  @quickeditmenu
] "^f8materials"

newgui brushes [
  @brushmenu
  guitab "^f8misc"
  @quickeditmenu
] "^f8brushes"

newgui quickedit [
  @quickeditmenu
  guitab "^f8materials"
  @matmenu
] "^f8quickedit"

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model" "0 360 0 100"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X" "0 200 0 200 0 200"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [
    if $hmapedit [showgui brushes] [
        if (enthavesel) [
            showgui (enttype)
        ] [showgui (? (havesel) materials quickedit)]
    ]
]

showentgui = [ contexteditgui ] // legacy bind

newgui seltexinfo [
    guitext (concatword "Slot: " (getseltex))
    guilist [ loop x 5 [
        texname = (gettexname (getseltex) $x)
        texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
        guiimage (concatword "packages/" $texname) "" 1 1
    ]]
    loop x 5 [ tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
]
exec "data/guilib.cfg"