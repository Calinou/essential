// console language standard library

// creates a macro whose body is a format string
// i.e. macro greet [ say Hi, %1! ]
macro = [
    $arg1 = (concat [format [@@arg2]] (loopconcat i $numargs [concatword " $arg" (+ $i 1)]))
]

// binds a key so that it will toggle a variable
// i.e. bindvar 9 thirdperson
bindvar = [
  bind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]
bindvarquiet = [
  bind $arg1 [@arg2 (= $@arg2 0)]
]

// same as above, but only binds for edit mode
editbindvar = [
  editbind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]
editbindvarquiet = [
  editbind $arg1 [@arg2 (= $@arg2 0)]
]

// binds a key so that it will set a modifier while held down
bindmod = [
  bind $arg1 [@arg2 1; onrelease [@@arg2 0]]
]

// same as above, but only binds for edit mode
editbindmod = [
  editbind $arg1 [@arg2 1; onrelease [@@arg2 0]]
]

quine = [ echo (format "quine = [%1]" $quine) ]

loadsky = [
    skybox $arg1
    if (> $numargs 1) [
        spinsky $arg2
    ] [
        if (!=f $spinsky 0) [spinsky 0]
    ]
]

mapmsg = [ maptitle $arg1 ]

//--Tomatenquark--
// generates a string of tabs
// i.e. echo (concatword (gentabs 2) "hello" (gentabs 5) "world")
gentabs = [
  _gtabs = ""
  loop t $arg1 [_gtabs = (concatword $_gtabs "^t")]
  result $_gtabs
]

// moves a gui item to any position
// i.e. guiitemmove 0 -5 -15 [guititle "Hi"]
guiitemmove = [guialign $arg1 [guistrut @arg2; guilist [guistrut @arg3; @arg4]]]

// add a new item to a list
// i.e. echo (listadd [a c d] "d")
listadd = [result (concatword $arg1 " ^"" $arg2 "^"" )]

// replaces an existing item in a list by its position
// i.e. echo (listreplace [a c c] 1 "b")
listreplace = [result (listsplice $arg1 $arg3 $arg2 1)]

// returns number of occurrences of an item in a list
// i.e. echo (listoccount [item1 item2 item3 item2 item4 item2] "item2")
listoccount = [
  _listoc = 0
  looplist i $arg2 [
    if (=s $arg1 $i)[_listoc = (+ $_listoc 1)]
  ]
  result $_listoc
]

// returns random item from given list
// i.e. echo (rndlist [hola hello ol√° ahoy])
rndlist = [at $arg1 (rnd (listlen $arg1))]

// returns number of occurrences of a character in a string
// i.e. echo (stroccount "hello world" "o")
stroccount = [
  _stroc = 0
  loop i (strlen $arg1) [
    if (=s (substr $arg1 $i 1) $arg2)[_stroc = (+ $_stroc 1)]
  ]
  result $_stroc
]

// returns an unformatted string
// i.e. echo (strreform "^f2hello^n^tworld^7!^f3!")
strreform = [
  _strre = (strreplace $arg1 "^n" "")
  _strre = (strreplace $arg1 "^t" "")
  loop i 9 [
    _strre = (strreplace $_strre (concatword "^f" $i) "")
  ]
  result $_strre
]